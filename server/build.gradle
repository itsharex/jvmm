plugins {
    id 'java'
    id 'application'
}
ext.projectName = 'jvmm-server'
archivesBaseName = "jvmm-server"

group = rootProject.group
version = rootProject.version

dependencies {
    compile(project(':core'))
    compile(project(':convey'))
    compile "org.yaml:snakeyaml:${snakeyamlVersion}"
}

task cleanJar(type: Delete) {
    delete 'src/main/resources/jvmm-logger.jar'
}

tasks.clean.configure {
    dependsOn cleanJar
}

tasks.compileJava.configure {
    dependsOn project(':logger').tasks.jar

    doLast {

        delete 'src/main/resources/jvmm-logger.jar'

        // 发布时去掉
        println("copy logger jar file to resources")
        copy {
            from '../logger/build/libs/jvmm-logger.jar'
            into 'src/main/resources'
        }
    }
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'

    archiveName("jvmm-server.jar")

    manifest {
        attributes 'Main-Class': 'org.beifengtz.jvmm.server.ServerApplication'
    }

    //  发布时去掉
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    doLast {
        def targetDir = "${rootProject.rootDir}/bin/jvmm-${rootProject.version}"

        copy {
            println("copy config.yml to ${targetDir}")

            from "${project.buildDir}/resources/main/config.yml"
            into targetDir
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveName("jvmm-server-sources.jar")
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

tasks.jar.configure {
    dependsOn sourcesJar
}

artifacts {
    archives sourcesJar
}

task javadocJar(type: Jar) {
    from javadoc
    getArchiveClassifier().set('javadoc')
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}
